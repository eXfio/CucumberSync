apply plugin: 'com.android.application'

//Helper methods
configure(project.rootProject) {
    ext.getEnvOrPrompt = { key,msg,isPassword,defValue ->
        if (System.getenv(key)) {
            System.getenv(key)
        } else if (System.hasProperty('console') && System.console() != null) {
            if (isPassword) {
                new String(System.console().readPassword("\n\$ " + msg + ": "))
            } else {
                System.console().readLine("\n\$ " + msg + ": ")
            }
        } else {
            defValue
        }
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "org.exfio.weavedroid"
        minSdkVersion 14
        targetSdkVersion 21
        multiDexEnabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        //incremental true
        javaMaxHeapSize "2G"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'commons-codec.license'
    }

    signingConfigs {

        release {
            storeFile     file(getEnvOrPrompt("KEYSTORE", "Enter keystore path", false, "android.keystore"))
            storePassword getEnvOrPrompt("KEYSTORE_PASSWORD", "Enter keystore password", true, null)
            keyAlias      getEnvOrPrompt("KEY_ALIAS", "Enter key alias", false, null)
            keyPassword   getEnvOrPrompt("KEY_PASSWORD", "Enter key password", true, null)
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    //Android
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:support-v4:22.2.0'

    //Override older versions of junit in dependencies
    compile 'junit:junit:4.12'

    //weaveclient library
    compile('org.exfio:weaveclient:1.0.0-alpha-3') {
        //exclude android provided and non-android libraries
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'stax', module: 'stax'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }

    compile 'org.apache.httpcomponents:httpclient-android:4.3.3'
    compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.4'
    compile ('org.mnode.ical4j:ical4j:1.0.6') {
        //exclude Android provided libraries
        exclude group: 'backport-util-concurrent', module: 'backport-util-concurrent'
    }
    compile 'net.sf.biweekly:biweekly:0.4.2'
    compile 'com.googlecode.ez-vcard:ez-vcard:0.9.6'

    provided 'org.projectlombok:lombok:1.16.4'
}
