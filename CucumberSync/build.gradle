apply plugin: 'com.android.application'

//Helper methods
configure(project.rootProject) {
    ext.getEnvOrPrompt = { key,msg,isPassword,defValue ->
        if (System.getenv(key)) {
            System.getenv(key)
        } else if (System.hasProperty('console') && System.console() != null) {
            if (isPassword) {
                new String(System.console().readPassword("\n\$ " + msg + ": "))
            } else {
                System.console().readLine("\n\$ " + msg + ": ")
            }
        } else {
            defValue
        }
    }

    ext.getEnvOrProperty = { key,msg,isPassword,defValue ->
        if (System.getenv(key)) {
            System.getenv(key)
        } else if (project.hasProperty(key)) {
            project.getProperty(key)
        } else {
            defValue
        }
    }

    ext.getSemanticVersionName = { major,minor,buildType,buildNum ->
        if (buildType != "") {
            new String(major + "." + minor + "-" + buildType + "-" + buildNum)
        } else {
            new String(major + "." + minor)
        }
    }
}

def versionMajor     = 1
def versionMinor     = 4
def versionBuildType = "beta"
def versionBuild     = 1

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "org.exfio.csyncdroid"
        versionCode 8
        versionName = "1.4-beta-1"
        //versionName getSemanticVersionName(${versionMajor}, ${versionMinor}, ${versionBuildType}, ${versionBuild})

        minSdkVersion 14
        targetSdkVersion 21
        multiDexEnabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        //incremental true
        javaMaxHeapSize "2G"
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'commons-codec.license'
    }

    signingConfigs {
        release {
            storeFile     file(project.getProperty('storeFile'))
            storePassword project.getProperty('storePassword')
            keyAlias      project.getProperty('keyAlias')
            keyPassword   project.getProperty('keyPassword')
        }
    }

    buildTypes {
        debug {
            minifyEnabled false

            buildConfigField "String", "LOG_LEVEL",                    "\"trace\""
            buildConfigField "String", "CSYNC_DEFAULT_ACCOUNT_SERVER", "\"" + project.getProperty('debugCSyncAccountServer') + "\""
            buildConfigField "String", "CSYNC_DEFAULT_TOKEN_SERVER",   "\"" + project.getProperty('debugCSyncTokenServer') + "\""
            buildConfigField "String", "FXA_DEFAULT_ACCOUNT_SERVER",   "\"" + project.getProperty('debugFxaAccountServer') + "\""
            buildConfigField "String", "FXA_DEFAULT_TOKEN_SERVER",     "\"" + project.getProperty('debugFxaTokenServer') + "\""
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release

            buildConfigField "String", "LOG_LEVEL",                    "\"warn\""
            buildConfigField "String", "CSYNC_DEFAULT_ACCOUNT_SERVER", "\"https://api.accounts.cucumbersync.com/v1\""
            buildConfigField "String", "CSYNC_DEFAULT_TOKEN_SERVER",   "\"https://cucumbersync.com/token/1.0/sync/1.5\""
            buildConfigField "String", "FXA_DEFAULT_ACCOUNT_SERVER",   "\"https://api.accounts.firefox.com/v1\""
            buildConfigField "String", "FXA_DEFAULT_TOKEN_SERVER",     "\"https://cucumbersync.com/token/1.0/sync/1.5\""
        }
    }
}

dependencies {
    //Android
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.android.support:support-v4:22.2.0'

    //Override older versions of junit in dependencies
    compile 'junit:junit:4.12'

    //weaveclient library
    compile('org.exfio:weaveclient:1.2.0-alpha-1') {
        //exclude android provided and non-android libraries
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'org.apache.httpcomponents', module: 'httpcore'
        exclude group: 'org.xerial', module: 'sqlite-jdbc'
        exclude group: 'org.bouncycastle', module: 'bcprov-jdk15on'
        exclude group: 'stax', module: 'stax'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }

    compile 'org.apache.httpcomponents:httpclient-android:4.3.3'
    compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'com.madgag.spongycastle:prov:1.51.0.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.4'
    compile 'net.sf.biweekly:biweekly:0.4.2'
    compile 'com.googlecode.ez-vcard:ez-vcard:0.9.6'

    provided 'org.projectlombok:lombok:1.16.4'
}
